plugins {
	id 'org.springframework.boot' version '2.7.4'
	id 'io.spring.dependency-management' version '1.0.14.RELEASE'
	id "com.github.node-gradle.node" version "3.1.0"
	id 'java'
}

// 사용하고자하는 node 정보 추가
node {
	version = '16.18.0'
	npmVersion = '8.19.2'
	download = true
	workDir = file("${project.buildDir}/nodejs")
	npmWorkDir = file("${project.buildDir}/npm")
}

//이미 빌드된 파일 삭제
task deleteVueArtifact(type: Delete) {
	delete "src/main/resources/static"
}

//npm install
task frontNpmInstall(type: NpmTask) {
	workingDir = file("${project.projectDir}/vuefront")
	args = ['install']
}

//npm run build
task npmBuild(type: NpmTask, dependsOn: ['deleteVueArtifact', 'frontNpmInstall']) {
	workingDir = file("${project.projectDir}/vuefront")
	args = ['run', 'build']
}

processResources.dependsOn 'npmBuild'

group = 'com.wiseinc'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}
